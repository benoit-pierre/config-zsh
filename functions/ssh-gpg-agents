# SSH/GPG agents handling.

ssh-gpg-agents()
{
  if which keychain &>/dev/null
  then
    cmd=(keychain --inherit any)
    mode='start'
    agents=()
    [[ ( -z "$SSH_CONNECTION" || -z "$SSH_AUTH_SOCK" ) && -f "$HOME/.ssh/noagent" ]] || agents+='ssh'
    [[ -f "$HOME/.gnupg/noagent" ]] || agents+='gpg'
    if [[ 0 -eq $#agents ]]
    then
      return 0
    fi
    cmd+=(--agents "${(j|,|)agents}")
    if [[ 'x-k' = "x$1" ]]
    then
      mode='stop'
      cmd+=(--stop mine)
    fi
    "$cmd[@]"
    code=$?
    if [[ 'start' = "$mode" ]]
    then
      [[ -n "${agents[(ke)gpg]}" && -f "$HOME/.keychain/$HOST-sh-gpg" ]] && . "$HOME/.keychain/$HOST-sh-gpg"
      [[ -n "${agents[(ke)ssh]}" && -f "$HOME/.keychain/$HOST-sh" ]] && . "$HOME/.keychain/$HOST-sh"
    fi
    return $code
  else
    echo 1>&2 'Keychain is not installed! SSH/GPG agents handling disabled.'
    return 255
  fi
}

ssh-add()
{
  if [[ -z "$SSH_AGENT_PID" && -z "$SSH_AUTH_SOCK" ]]
  then
    ssh-gpg-agents
  fi
  if [[ 0 -eq "$#" ]]
  then
    command ssh-add -l | grep " $HOME/.ssh/id\(_rsa\|_dsa\|entity\) " && return 0
  fi
  command ssh-add "$@"
}

# vim: ft=zsh
